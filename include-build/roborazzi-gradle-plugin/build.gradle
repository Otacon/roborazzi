plugins {
  id "org.jetbrains.kotlin.jvm"
  id 'jvm-test-suite'
}
if (System.getenv("INTEGRATION_TEST") != "true") {
  pluginManager.apply("com.vanniktech.maven.publish")
}

apply plugin: 'java-gradle-plugin'

def integrationTestDep = sourceSets.create('integrationTestDep')

def generatedSourcesDir = "${project.layout.buildDirectory.get()}/generated/sources/buildConfig/java/main"
sourceSets {
  main {
    java {
      srcDir(generatedSourcesDir)
    }
  }
}

tasks.register("generateBuildConfig") {
  def libs = ["composable-preview-scanner", "junit", "robolectric"]
  def versionMapString = "\"roborazzi\" to \"$VERSION_NAME\", \n" + (
    libs.collect {
      def version = project.extensions.getByType(VersionCatalogsExtension.class)
        .named("libs")
        .findVersion(it)
        .get()
      "\"${it}\" to \"${version}\","
    }.join("\n"))
  inputs.property("versionMapString", versionMapString)
  doLast {
    def buildConfigFile = new File(generatedSourcesDir, "io/github/takahirom/roborazzi/BuildConfig.kt")
    buildConfigFile.parentFile.mkdirs()

    def libString = "val libraryVersionsMap = mapOf(" + inputs.properties["versionMapString"] + ")\n"
    ""
    buildConfigFile.text = """
package io.github.takahirom.roborazzi
class BuildConfig {
    companion object {
        ${libString}
    }
}
        """
  }
}

tasks.named("compileKotlin") {
  dependsOn("generateBuildConfig")
}


dependencies {
  compileOnly gradleApi()
  compileOnly libs.android.tools.build.gradle
  compileOnly libs.kotlin.gradle.plugin
  implementation(project(':roborazzi-core')) {
    // We don't use junit for plugin
    exclude group: 'junit', module: 'junit'
  }
  implementation libs.kotlinx.serialization.json
  implementation libs.webjars.locator.lite
  implementation libs.webjars.materialize
  implementation libs.webjars.material.design.icons
  integrationTestDepImplementation libs.android.tools.build.gradle
  integrationTestDepImplementation libs.kotlin.gradle.plugin
  integrationTestDepImplementation libs.compose.gradle.plugin
}

sourceSets {
  main.java.srcDir 'src/generated/kotlin'
}

test {
  testLogging {
    events "passed", "skipped", "failed", "standardOut", "standardError"
    showStandardStreams = true
    showStackTraces = true
    exceptionFormat = 'full'
  }
}


testing.suites {
  integrationTest(JvmTestSuite) {
    useJUnit()
    sources.kotlin.srcDirs = ["src/integrationTest/java"]
    sources.java.srcDirs = ["src/integrationTest/java"]
    dependencies {
//      implementation project()
      implementation gradleTestKit()
      implementation project(':roborazzi-core')
      implementation libs.junit
      implementation libs.android.tools.build.gradle
      implementation libs.kotlin.gradle.plugin
      implementation libs.compose.gradle.plugin
    }

    targets {
      all {
        testTask.configure {
          shouldRunAfter(test)
          testLogging {
            events "failed", "standardError"
            showStandardStreams = true
            showStackTraces = true
            exceptionFormat = 'full'
          }
        }
      }
    }
    tasks.withType(PluginUnderTestMetadata.class).named("pluginUnderTestMetadata").configure {
      it.pluginClasspath.from(integrationTestDep.runtimeClasspath)
    }
  }
}

gradlePlugin {
  testSourceSets sourceSets.integrationTest
}

gradlePlugin {
  plugins {
    roborazzi {
      id = 'io.github.takahirom.roborazzi'
      implementationClass = 'io.github.takahirom.roborazzi.RoborazziPlugin'
    }
  }
}


tasks.named('check') {
  dependsOn(testing.suites.integrationTest)
}

